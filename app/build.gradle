apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'


def getVersionCode = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'tag', '--list'
        standardOutput = stdout
    }
    return stdout.toString().split("\n").size() + 1800
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--dirty', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "igrek.songbook"
        minSdkVersion 19 // Android 4.4 KitKat
        targetSdkVersion 33 // Android 13
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    signingConfigs {
        release {
            def propsFile = rootProject.file('.keystore.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "java.util.Date", "BUILD_DATE", "new java.util.Date(" + System.currentTimeMillis() + "L)"
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            buildConfigField "java.util.Date", "BUILD_DATE", "new java.util.Date(" + System.currentTimeMillis() + "L)"
            debuggable true
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
    compileOptions {
        sourceCompatibility '1.8'
        targetCompatibility '1.8'
        incremental = true
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
    kotlinOptions {
        freeCompilerArgs = ["-Xallow-result-return-type", "-opt-in=kotlin.RequiresOptIn"]
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // Android
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.annotation:annotation:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.activity:activity-ktx:1.7.1'
    implementation 'androidx.work:work-runtime:2.8.1'
    // Material Design UI components
    implementation 'com.google.android.material:material:1.9.0'
    // Firebase
    implementation 'com.google.firebase:firebase-analytics-ktx:21.2.2'
    implementation 'com.google.firebase:firebase-crashlytics:18.3.7'
    // Google APIs
    implementation('com.google.api-client:google-api-client-android:1.35.2') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation 'com.google.android.gms:play-services-basement:18.2.0'
    implementation('com.google.android.gms:play-services-auth:20.5.0') {
    // AdMob
    implementation 'com.google.android.gms:play-services-ads:21.5.0'
    }
    implementation('com.google.http-client:google-http-client-gson:1.43.1') {
        exclude module: 'httpclient'
        exclude module: 'commons-logging'
    }
    implementation('com.google.apis:google-api-services-drive:v3-rev197-1.25.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    // Google Play Billing
    implementation 'com.android.billingclient:billing-ktx:5.2.0'
    // Kotlin
    implementation "androidx.core:core-ktx:1.10.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime:1.0-M1-1.4.0-rc'
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    // Jackson
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.3'
    // Guava
    implementation 'com.google.guava:guava:31.1-android'
    // Apache Commons Codec
    implementation 'commons-codec:commons-codec:1.15'
    // Protobuf
    implementation 'com.google.protobuf:protobuf-kotlin:3.22.2'
    // RX
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    // OkHttp
    implementation 'com.squareup.okhttp3:okhttp:3.12.12' // last version supporting API 19
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.12'
    // JWT
    implementation 'com.auth0.android:jwtdecode:2.0.2'
    // Socket.IO
    implementation ('io.socket:socket.io-client:2.1.0') {
        exclude group: 'org.json', module: 'json'
    }
    // PDFBox
    implementation 'com.tom-roush:pdfbox-android:2.0.25.0' // don't upgrade due to huge bundle size
    // Unit tests
    testImplementation 'androidx.appcompat:appcompat:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    // Android instumentation tests
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    androidTestImplementation 'org.mockito:mockito-core:2.28.2'
    androidTestImplementation 'org.mockito:mockito-android:2.22.0'
    androidTestImplementation 'org.assertj:assertj-core:3.24.2'
}
